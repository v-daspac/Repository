

# Document.SaveAs2, méthode (Word)

Enregistre le document spécifié sous un nouveau nom ou format. Certains arguments utilisés pour cette méthode correspondent aux options de la boîte de dialogue  **Enregistrer sous** (onglet **Fichier**).
 


## Syntaxe

 *expression*  . **SaveAs2**( ** *FileName* **, ** *FileFormat* **, ** *LockComments* **, ** *Password* **, ** *AddToRecentFiles* **, ** *WritePassword* **, ** *ReadOnlyRecommended* **, ** *EmbedTrueTypeFonts* **, ** *SaveNativePictureFormat* **, ** *SaveFormsData* **, ** *SaveAsAOCELetter* **, ** *Encoding* **, ** *InsertLineBreaks* **, ** *AllowSubstitutions* **, ** *LineEnding* **, ** *AddBiDiMarks* **, ** *CompatibilityMode* ** )
 

 
 *expression*  Expression qui renvoie un objet ** [Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)**.
 

 

### Paramètres



|**Nom**|**Obligatoire/Facultatif**|**Type de données**|**Description**|
|:-----|:-----|:-----|:-----|
| _FileName_|Facultatif|**Variant**|Nom du document. Il s'agit par défaut du nom de dossier et de fichier actif. Si le document n'a jamais été enregistré, le nom par défaut est utilisé (par exemple, Doc1.doc). Si un document portant le nom de fichier spécifié existe déjà, celui-ci est remplacé sans que l'utilisateur en soit averti.|
| _FileFormat_|Facultatif|**Variant**|Format sous lequel le document est enregistré. Il peut s'agir de toute constante de l'énumération  ** [WdSaveFormat](eb8d5689-9b4b-8d09-321c-1a3c1ee777e9.md)**. Pour enregistrer un document dans un autre format, définissez la valeur appropriée pour la propriété ** [SaveFormat](d837cd22-38eb-5160-1f85-16001448213e.md)** de l'objet ** [FileConverter](41af2a9b-75cc-253d-4954-4fb42c88530f.md)**.|
| _LockComments_|Facultatif|**Variant**|**True** pour verrouiller le document afin d'empêcher l'insertion de commentaires. La valeur par défaut est **False**.|
| _Password_|Facultatif|**Variant**|Chaîne de mot de passe permettant l'ouverture du document. (Voir les remarques ci-dessous.)|
| _AddToRecentFiles_|Facultatif|**Variant**|**True** pour ajouter le document à la liste des derniers fichiers utilisés, qui s'affiche en bas du menu **Fichiers**. La valeur par défaut est  **True**.|
| _WritePassword_|Facultatif|**Variant**|Chaîne de mot de passe permettant l'enregistrement des modifications apportées au document. (Voir les remarques ci-dessous.)|
| _ReadOnlyRecommended_|Facultatif|**Variant**|**True** pour que Microsoft Word propose le statut lecture seule à chaque ouverture du document. La valeur par défaut est **False**.|
| _EmbedTrueTypeFonts_|Facultatif|**Variant**|**True** pour enregistrer les polices TrueType avec le document. Si cet argument est omis, l'argument EmbedTrueTypeFonts prend la valeur de la propriété ** [EmbedTrueTypeFonts](ac8fb6a1-584a-2ddb-4216-53e30473ff65.md)**.|
| _SaveNativePictureFormat_|Facultatif|**Variant**|Si des graphiques ont été importés à partir d'une autre plateforme (par exemple, Macintosh),  **True** pour enregistrer uniquement la version Microsoft Windows des graphiques importés.|
| _SaveFormsData_|Facultatif|**Variant**|**True** pour enregistrer les données entrées par un utilisateur dans un formulaire en tant qu'enregistrement.|
| _SaveAsAOCELetter_|Facultatif|**Variant**|Si le document a un publipostage associé,  **True** pour enregistrer le document sous la forme d'une lettre AOCE (le publipostage est enregistré).|
| _Encoding_|Facultatif|**Variant**|Page de code ou jeu de caractères à utiliser pour les documents enregistrés au format de fichier texte codé. La valeur par défaut est la page de code système. Vous ne pouvez pas utiliser toutes les constantes  ** [MsoEncoding](http://msdn.microsoft.com/library/286bed6e-6028-a252-5e4f-b505234d9d34%28Office.15%29.aspx)** avec ce paramètre.|
| _InsertLineBreaks_|Facultatif|**Variant**|Si le document est enregistré en tant que fichier texte,  **True** pour insérer un saut de ligne à la fin de chaque ligne de texte.|
| _AllowSubstitutions_|Facultatif|**Variant**|Si le document est enregistré en tant que fichier texte, la valeur  **True** permet à Word de remplacer certains caractères spéciaux par du texte d'aspect similaire. Un exemple consiste à afficher le symbole de copyright sous la forme (c). La valeur par défaut est **False**.|
| _LineEnding_|Facultatif|**Variant**|Manière dont Word marque les sauts de ligne et de paragraphe dans les documents enregistrés au format de fichier texte. Il peut s'agir de l'une des constantes  ** [WdLineEndingType](6a0d8e41-4248-e48f-1db6-77fa59a6f212.md)** suivantes : **wdCRLF** (par défaut) ou **wdCROnly**.|
| _AddBiDiMarks_|Facultatif|**Variant**|**True** ajoute des caractères de contrôle au fichier de sortie pour conserver la disposition bi-directionnelle du texte dans le document d'origine.|
| _CompatibilityMode_|Facultatif|**Variant**|Mode de compatibilité utilisé par Word à l'ouverture du document. Constante  ** [WdCompatibilityMode](244e1519-32ea-741c-7e2a-ff8a2e0713aa.md)**. **Importante**  Par défaut, si aucune valeur n'est définie pour ce paramètre, Word entre une valeur de 0, qui spécifie que le mode de compatibilité actuel du document doit être conservé. |

### Valeur renvoyée

Aucune
 

 

## Exemple

L'exemple de code suivant enregistre le document actif au format texte enrichi (RTF) sous le nom Test.rtf.
 

 

```
Sub SaveAsRTF() 
    ActiveDocument.SaveAs2 FileName:="Text.rtf", _ 
        FileFormat:=wdFormatRTF 
End Sub
```

L'exemple de code suivant enregistre le document actif au format de fichier texte avec l'extension « .txt ».
 

 



```
Sub SaveAsTextFile() 
    Dim strDocName As String 
    Dim intPos As Integer 
 
    ' Find position of extension in file name 
    strDocName = ActiveDocument.Name 
    intPos = InStrRev(strDocName, ".") 
 
    If intPos = 0 Then 
 
        ' If the document has not yet been saved 
        ' Ask the user to provide a file name 
        strDocName = InputBox("Please enter the name " &amp; _ 
            "of your document.") 
    Else 
 
        ' Strip off extension and add ".txt" extension 
        strDocName = Left(strDocName, intPos - 1) 
        strDocName = strDocName &amp; ".txt" 
    End If 
 
    ' Save file with new extension 
    ActiveDocument.SaveAs2 FileName:=strDocName, _ 
        FileFormat:=wdFormatText 
End Sub
```

L'exemple de code suivant recherche tous les convertisseurs installés et, si le convertisseur WordPerfect 6.0 figure parmi ceux-ci, utilise celui-ci pour enregistrer le document actif.
 

 



```
Sub SaveWithConverter() 
 
    Dim cnvWrdPrf As FileConverter 
 
    ' Look for WordPerfect file converter 
    ' And save document using the converter 
    ' For the FileFormat converter value 
    For Each cnvWrdPrf In Application.FileConverters 
        If cnvWrdPrf.ClassName = "WrdPrfctWin" Then 
            ActiveDocument.SaveAs2 FileName:="MyWP.doc", _ 
                FileFormat:=cnvWrdPrf.SaveFormat 
        End If 
    Next cnvWrdPrf 
 
End Sub
```

L'exemple de code suivant montre une procédure qui enregistre un document avec un mot de passe.
 

 



```
Sub SaveWithPassword(docCurrent As Document, strPWD As String) 
    With docCurrent 
        .SaveAs2 WritePassword:=strPWD 
    End With 
End Sub
```


## Voir aussi


#### Concepts


 
 [Objet Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)
#### Autres ressources


 
 [Membres de l'objet Document](fc9ab457-0888-f917-3d52-387168ac23b9.md)