
# Form.Error-Ereignis (Access)

Das Error-Ereignis tritt ein, wenn in Microsoft Access ein Laufzeitfehler erzeugt wird, während ein Formular den Fokus hat.
 


## Syntax

 *Ausdruck*  . **Error**( ** *DataErr* **, ** *Response* ** )
 

 
 *Ausdruck*  Eine Variable, die ein **Form** -Objekt darstellt.
 

 

### Parameter



|**Name**|**Erforderlich/Optional**|**Datentyp**|**Beschreibung**|
|:-----|:-----|:-----|:-----|
| _DataErr_|Erforderlich|**Integer**|Der Fehlercode, der vom Err-Objekt beim Eintreten eines Fehlers zurückgegeben wird. Sie können das Argument DataErr in der Error-Funktion verwenden, um die Fehlernummer der entsprechenden Fehlermeldung zuzuordnen.|
| _Response_|Erforderlich|**Integer**| Diese Einstellung bestimmt, ob eine Fehlermeldung angezeigt wird. Das Response-Argument kann eine der folgenden systeminternen Konstanten sein: <BR/>**acDataErrContinue** Ignoriert den Fehler und setzt den Vorgang ohne Anzeige der Standardfehlermeldung von Microsoft Access fort. Sie können statt der Standardfehlermeldung eine benutzerdefinierte Fehlermeldung anzeigen. <BR/>**acDataErrDisplay** (Standardeinstellung) Zeigt die Standardfehlermeldung von Microsoft Access an.|

## Bemerkungen

Dies gilt auch für Fehler des Microsoft Access-Datenbankmoduls, nicht jedoch für Laufzeitfehler in Visual Basic oder Fehler von ADO.
 

 
Wenn beim Auftreten dieses Ereignisses ein Makro oder eine Ereignisprozedur ausgeführt werden soll, legen Sie die  **OnError** -Eigenschaft auf den Namen des Makros oder auf [Ereignisprozedur] fest.
 

 
Durch Ausführen einer Ereignisprozedur oder eines Makros bei Eintreten des Ereignisses Error können Sie eine Fehlermeldung von Microsoft Access abfangen und stattdessen eine benutzerdefinierte Meldung anzeigen, die für Ihre Anwendung geeigneter ist.
 

 
 **Link zur Verfügung gestellt von:**
 
![Symbol für Communitymitglied](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png)
 
 [UtterAccess](http://www.utteraccess.com)-Community
 

 

- Fehler
    
 

## Beispiel

Das folgende Beispiel zeigt, wie Sie eine Standardfehlermeldung durch eine benutzerdefinierte Fehlermeldung ersetzen können. Wenn Microsoft Access eine Fehlermeldung zurückgibt und darin mitteilt, dass ein Schlüsselduplikat gefunden wurde (Fehlercode 3022), zeigt diese Ereignisprozedur eine Meldung mit genaueren anwendungsspezifischen Informationen für den Benutzer an.
 

 
Zum Testen dieses Beispiels fügen Sie die nachstehende Ereignisprozedur zu einem Formular hinzu, das auf einer Tabelle mit einer eindeutigen Personalnummer als Schlüssel für die einzelnen Datensätze basiert.
 

 



```
Private Sub Form_Error(DataErr As Integer, Response As Integer) 
    Const conDuplicateKey = 3022 
    Dim strMsg As String 
 
    If DataErr = conDuplicateKey Then 
        Response = acDataErrContinue 
        strMsg = "Each employee record must have a unique " _ 
            &amp; "employee ID number. Please recheck your data." 
        MsgBox strMsg 
    End If 
End Sub
```


 

 

 
Das folgende Beispiel zeigt, wie Sie eine Standardfehlermeldung durch eine benutzerdefinierte Fehlermeldung ersetzen können.
 

 
 **Der Beispielcode stammt von:**
 
![MVP-Kontributor](images/odc_OfficeTA_33px_MVPContrib.jpg)
 
 Bill Jelen, [MrExcel.com](http://www.mrexcel.com/)
 

 



```
Private Sub Form_Error(DataErr As Integer, Response As Integer)
    Select Case DataErr
        Case 2113
            MsgBox "Only numbers are acceptable in this box", vbCritical, "Call 1-800-123-4567"
            Response = acDataErrContinue
        Case 2237
            MsgBox "You can only choose from the dropdown box"
            Response = acDataErrContinue
        Case 3022
            MsgBox "You entered a value that exists already in another record"
            Response = acDataErrContinue
            SSN.Value = SSN.OldValue
        Case 3314
            MsgBox "The DOH is required, so you cannot leave this field empty"
            Response = acDataErrContinue
        Case Else
            Response = acDataErrDisplay
    End Select
    ActiveControl.Undo
End Sub
```


## Informationen zu den Mitwirkenden
<a name="AboutContributors"> </a>

UtterAccess ist das führende Microsoft Access-Wiki und -Hilfeforum. Zum Beitreten hier klicken.
 

 
Holy Macro! Books veröffentlicht unterhaltsame Bücher für Benutzer von Microsoft Office. Den kompletten Katalog finden Sie unter MrExcel.com.
 

 

## Siehe auch
<a name="AboutContributors"> </a>


#### Konzepte


 
 [Form Object](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Weitere Ressourcen


 
 [Form Object Members](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)