
# ComboBox.NotInList-Ereignis (Access)

Das  **NotInList** -Ereignis tritt auf, wenn der Benutzer in das Textfeld eines Kombinationsfelds einen Wert eingibt, der nicht in der Liste des Kombinationsfelds enthalten ist.
 


## Syntax

 *Ausdruck*  . **NotInList**( ** *NewData* **, ** *Response* ** )
 

 
 *Ausdruck*  Eine Variable, die ein **ComboBox** -Objekt darstellt.
 

 

### Parameter



|**Name**|**Erforderlich/Optional**|**Datentyp**|**Beschreibung**|
|:-----|:-----|:-----|:-----|
| _NewData_|Erforderlich|**String**|Eine Zeichenfolge, mit der Microsoft Access den Text, der vom Benutzer in das Textfeld des Kombinationsfelds eingegeben wurde, an die Ereignisprozedur übergibt.|
| _Response_|Erforderlich|**Integer**| Diese Einstellung gibt an, wie auf das **NotInList** -Ereignis reagiert wurde. Das Argument _Response_ kann eine der folgenden systeminternen Konstanten sein: **acDataErrDisplay** (Standardeinstellung) Zeigt die Standardmeldung für den Benutzer an. Sie können diese Konstante verwenden, wenn der Benutzer keine Möglichkeit haben soll, einen neuen Wert zur Kombinationsfeldliste hinzuzufügen. **acDataErrContinue** zeigt nicht die Standardmeldung für den Benutzer an. Diese Konstante können Sie verwenden, wenn Sie eine benutzerdefinierte Meldung für den Benutzer anzeigen möchten. So könnte die Ereignisprozedur beispielsweise ein benutzerdefiniertes Dialogfeld mit der Frage anzeigen, ob der Benutzer den neuen Eintrag speichern möchte. Wenn die Antwort "Ja" lautet, würde die Ereignisprozedur den neuen Eintrag zur Liste hinzufügen und das Argument _Response_ auf **acDataErrAdded** festlegen. Wenn die Antwort "Nein" lautet, würde die Ereignisprozedur das Argument _Response_ auf **acDataErrContinue** festlegen. **acDataErrAdded** zeigt keine Meldung für den Benutzer an, ermöglicht es jedoch, den Eintrag zur Kombinationsfeldliste in der **NotInList** -Ereignisprozedur hinzuzufügen. Nachdem der Eintrag hinzugefügt wurde, aktualisiert Microsoft Access die Liste durch erneutes Abfragen des Kombinationsfelds. Anschließend überprüft Microsoft Access die Zeichenfolge erneut anhand der Kombinationsfeldliste und speichert den Wert des Arguments _NewData_ in dem Feld, an das das Kombinationsfeld gebunden ist. Wenn die Zeichenfolge nicht in der Liste enthalten ist, zeigt Microsoft Access eine Fehlermeldung an.|

## Bemerkungen

Wenn beim Eintreten dieses Ereignisses ein Makro oder eine Ereignisprozedur ausgeführt werden soll, legen Sie die  ** [OnNotInList](307e9f0c-6db7-b995-166b-060c697b9f6e.md)** -Eigenschaft auf den Namen des Makros oder auf [Ereignisprozedur] fest.
 

 
Dieses Ereignis ermöglicht dem Benutzer, einen neuen Wert zum Listenfeld eines Kombinationsfelds hinzuzufügen.
 

 
Das  **NotInList** -Ereignis kann nur auftreten, wenn die ** [LimitToList](885ed814-6e04-b9f1-0acb-3ded28e00f93.md)** -Eigenschaft auf "Ja" festgelegt ist.
 

 
Das  **NotInList** -Ereignis löst nicht das **Error** -Ereignis aus.
 

 
Das  **NotInList** -Ereignis tritt für Kombinationsfelder ein, deren **LimitToList** -Eigenschaft auf "Ja" festgelegt ist, und zwar wenn Sie einen Wert eingeben, der nicht in der Liste enthalten ist, und versuchen, zu einem anderen Steuerelement zu wechseln oder den Datensatz zu speichern. Es tritt nach allen **Change** -Ereignissen für das Kombinationsfeld ein.
 

 
Wenn die  ** [AutoExpand](0b3fabf8-4004-0868-3ddc-aef297514324.md)** -Eigenschaft auf "Ja" festgelegt ist, wählt Microsoft Access in der Liste die Einträge aus, die mit der fortlaufenden Eingabe des Benutzers im Textfeldteil des Kombinationsfelds übereinstimmen. Entspricht die Eingabe dem Anfang eines Listeneintrags (Beispiel: der Benutzer gibt "Schm" ein, und "Schmittz" kommt in der Liste vor), so tritt das **NotInList**-Ereignis nicht auf, wenn der Benutzer zu einem anderen Steuerelement übergeht oder den Datensatz speichert. Die Zeichen, die Microsoft Access zur Eingabe des Benutzers hinzufügt (im obigen Beispie "ittz"), werden jedoch im Textfeldteil des Kombinationsfelds ausgewählt. Wenn der Benutzer in solchen Fällen das  **NotInList** -Ereignis auslösen möchte (um beispielsweise den Namen "Schmitt" zur Kombinationsfeldliste hinzuzufügen), muss er hinter dem letzten Zeichen des neuen Werts ein **LEERZEICHEN** eingeben oder die **RÜCKTASTE** oder **ENTF** drücken.
 

 
Wenn die  **LimitToList** -Eigenschaft auf "Ja" festgelegt ist und die Dropdown-Kombinationsfeldliste geöffnet wird, wählt Microsoft Access in der Liste genau die Zeichenfolgen aus, die mit der fortlaufenden Eingabe des Benutzers im Textfeldteil des Kombinationsfelds übereinstimmen, selbst wenn die **AutoExpand** -Eigenschaft auf "Nein" festgelegt ist. Drückt der Benutzer die **EINGABETASTE**, oder wechselt er zu einem anderen Steuerelement oder Datensatz, so wird der ausgewählte Wert im Kombinationsfeld angezeigt. In diesem Fall wird das  **NotInList** -Ereignis nicht angewendet. Wenn das **NotInList** -Ereignis trotzdem ausgelöst werden soll, darf der Benutzer die Kombinationsfeldliste nicht öffnen.
 

 

## Beispiel

Das folgende Beispiel verwendet das Ereignis  **NotInList**, um einen Eintrag zu einem Kombinationsfeld hinzuzufügen.
 

 
Erstellen Sie zum Testen dieses Beispiels in einem Formular ein Kombinationsfeld namens "Colors". Legen Sie dessen  **LimitToList** -Eigenschaft auf "Ja" fest. Legen Sie zum Auffüllen des Kombinationsfelds dessen **RowSourceType** -Eigenschaft auf "Wertliste" fest, und geben Sie eine Liste von durch Semikolons getrennten Werten als Einstellung für die **RowSource** -Eigenschaft ein. Beispielsweise können Sie folgende Werte als Einstellung für diese Eigenschaft eingeben: Rot; Grün; Blau.
 

 
Fügen Sie anschließend die nachstehende Ereignisprozedur zum Formular hinzu. Wechseln Sie zur Formularansicht, und geben Sie einen neuen Wert in das Textfeld des Kombinationsfelds ein.
 

 

 **Hinweis**  Im vorstehenden Beispiel wird ein Eintrag zu einem ungebundenen Kombinationsfeld hinzugefügt. Wenn Sie einen Eintrag zu einem gebundenen Kombinationsfeld hinzufügen, fügen Sie damit einen Wert zu einem Feld in der zugrunde liegenden Datenquelle hinzu. In den meisten Fällen können Sie nicht einfach ein einziges Feld in einem neuen Datensatz hinzufügen, sondern müssen je nach der Struktur der Daten in der Tabelle wahrscheinlich ein oder mehrere Felder hinzufügen, um die Datenanforderungen zu erfüllen. Ein neuer Datensatz muss beispielsweise Werte für alle Felder enthalten, die den Primärschlüssel gefährden. Wenn Sie Einträge zu einem gebundenen Kombinationsfeld dynamisch hinzufügen müssen, muss der Benutzer aufgefordert werden, Daten für alle erforderlichen Felder einzugeben, den neuen Datensatz zu speichern und dann das Kombinationsfeld erneut abzufragen, um den neuen Wert anzuzeigen.
 




```
Private Sub Colors_NotInList(NewData As String, _ 
        Response As Integer) 
    Dim ctl As Control 
     
    ' Return Control object that points to combo box. 
    Set ctl = Me!Colors 
    ' Prompt user to verify they wish to add new value. 
    If MsgBox("Value is not in list. Add it?", _ 
         vbOKCancel) = vbOK Then 
        ' Set Response argument to indicate that data 
        ' is being added. 
        Response = acDataErrAdded 
        ' Add string in NewData argument to row source. 
        ctl.RowSource = ctl.RowSource &amp; ";" &amp; NewData 
    Else 
    ' If user chooses Cancel, suppress error message 
    ' and undo changes. 
        Response = acDataErrContinue 
        ctl.Undo 
    End If 
End Sub
```


 

 

 
Das folgende Beispiel zeigt, wie Sie das Ereignis  **NotInList** verwenden, um einen Eintrag zu einem gebundenen Kombinationsfeld hinzufügen.
 

 
 **Beispielcode bereitgestellt von:** Bill Jelen, [MrExcel.com](http://www.mrexcel.com/)
 

 



```
Private Sub cboDept_NotInList(NewData As String, Response As Integer)
    Dim oRS As DAO.Recordset, i As Integer, sMsg As String
    Dim oRSClone As DAO.Recordset

    Response = acDataErrContinue

    If MsgBox("Add dept?", vbYesNo) = vbYes Then
        Set oRS = CurrentDb.OpenRecordset("tblDepartments", dbOpenDynaset)
        oRS.AddNew
        oRS.Fields(1) = NewData
        For i = 2 To oRS.Fields.Count - 1
            sMsg = "What do you want for " &amp; oRS(i).Name
            oRS(i).Value = InputBox(sMsg, , oRS(i).DefaultValue)
        Next i
        oRS.Update
        cboDept = Null
        cboDept.Requery
        DoCmd.OpenTable "tblDepartments", acViewNormal, acReadOnly
        DoCmd.GoToRecord acDataTable, "tblDepartments", acLast
    End If
End Sub
```

Das folgende Beispiel zeigt, wie Sie einem gebundenen Kombinationsfeld ein Element hinzufügen.
 

 
 **Der Beispielcode stammt von:** [Microsoft Access 2010 Programmer's Reference](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)
 

 



```
Private Sub cboMainCategory_NotInList(NewData As String, Response As Integer)

    On Error GoTo Error_Handler
    Dim intAnswer As Integer
    intAnswer = MsgBox("""" &amp; NewData &amp; """ is not an approved category. " &amp; vbcrlf _
        &amp; "Do you want to add it now?" _ vbYesNo + vbQuestion, "Invalid Category")

    Select Case intAnswer
        Case vbYes
            DoCmd.SetWarnings False
            DoCmd.RunSQL "INSERT INTO tlkpCategoryNotInList (Category) "
                &amp; _ "Select """ &amp; NewData &amp; """;"
            DoCmd.SetWarnings True
            Response = acDataErrAdded
        Case vbNo
            MsgBox "Please select an item from the list.", _
                vbExclamation + vbOKOnly, "Invalid Entry"
            Response = acDataErrContinue

    End Select

    Exit_Procedure:
        DoCmd.SetWarnings True
        Exit Sub

    Error_Handler:
        MsgBox Err.Number &amp; ", " &amp; Error Description
        Resume Exit_Procedure
        Resume

End Sub
```


## Informationen zu den Mitwirkenden
<a name="AboutContributors"> </a>

Holy Macro! Books veröffentlicht unterhaltsame Bücher für Benutzer von Microsoft Office. Den kompletten Katalog finden Sie unter MrExcel.com.
 

 
Wrox Press wird getragen von der Philosophie "von Programmierern für Programmierer". Wrox-Bücher wurden von Programmierern für Programmierer verfasst, und die Marke Wrox steht für intelligente Lösungen für praktische Programmierprobleme.
 

 

## Siehe auch
<a name="AboutContributors"> </a>


#### Konzepte


 
 [ComboBox-Objekt](1cf508d5-023e-eb38-3991-71e82b2a4e7e.md)
#### Weitere Ressourcen


 
 [ComboBox-Objektelemente](d0d83ca3-3698-295e-5335-7d0816557d6b.md)