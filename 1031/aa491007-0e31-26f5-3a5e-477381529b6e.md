

# Document.SaveAs2-Methode (Word)

Speichert das angegebene Dokument unter einem neuen Namen oder in einem neuen Format. Einige Argumente für diese Methode entsprechen den Optionen im Dialogfeld  **Speichern unter** (Registerkarte **Datei**).
 


## Syntax

 *Ausdruck*  . **SaveAs2**( ** *FileName* **, ** *FileFormat* **, ** *LockComments* **, ** *Password* **, ** *AddToRecentFiles* **, ** *WritePassword* **, ** *ReadOnlyRecommended* **, ** *EmbedTrueTypeFonts* **, ** *SaveNativePictureFormat* **, ** *SaveFormsData* **, ** *SaveAsAOCELetter* **, ** *Encoding* **, ** *InsertLineBreaks* **, ** *AllowSubstitutions* **, ** *LineEnding* **, ** *AddBiDiMarks* **, ** *CompatibilityMode* ** )
 

 
 *Ausdruck*  Ein Ausdruck, der ein ** [Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)** -Objekt zurückgibt.
 

 

### Parameter



|**Name**|**Erforderlich/Optional**|**Datentyp**|**Beschreibung**|
|:-----|:-----|:-----|:-----|
| _FileName_|Optional|**Variant**|Der Name für das Dokument. Der Standardwert ist der aktuelle Ordner und der Dateiname. Wenn das Dokument noch nie gespeichert wurde, wird der Standardname verwendet (z. B. Doc1.doc). Wenn bereits ein Dokument mit dem angegebenen Dateinamen vorhanden ist, wird das Dokument überschrieben, ohne dass der Benutzer gefragt wird.|
| _FileFormat_|Optional|**Variant**|Das Format, in dem das Dokument gespeichert wird. Kann jede  ** [WdSaveFormat](eb8d5689-9b4b-8d09-321c-1a3c1ee777e9.md)** -Konstante sein. Zum Speichern eines Dokuments in einem anderen Format geben Sie den entsprechenden Wert für die ** [SaveFormat](d837cd22-38eb-5160-1f85-16001448213e.md)** -Eigenschaft des ** [FileConverter](41af2a9b-75cc-253d-4954-4fb42c88530f.md)** -Objekts an.|
| _LockComments_|Optional|**Variant**|**True**, wenn das Dokument für Kommentare gesperrt werden soll. Der Standardwert ist **False**.|
| _Password_|Optional|**Variant**|Eine Kennwortzeichenfolge zum Öffnen des Dokuments. (Siehe Hinweise weiter unten.)|
| _AddToRecentFiles_|Optional|**Variant**|**True**, wenn das Dokument der Liste der zuletzt verwendeten Dateien im Menü **Datei** hinzugefügt werden soll. Der Standardwert ist **True**.|
| _WritePassword_|Optional|**Variant**|Eine zum Speichern von Änderungen am Dokument erforderliche Kennwortzeichenfolge. (Siehe Hinweise weiter unten.)|
| _ReadOnlyRecommended_|Optional|**Variant**|**True**, wenn Microsoft Word beim Öffnen des Dokuments Schreibschutz vorschlagen soll. Der Standardwert ist **False**.|
| _EmbedTrueTypeFonts_|Optional|**Variant**|**True**, wenn TrueType-Schriftarten mit dem Dokument gespeichert werden sollen. Wenn der Parameter nicht angegeben wird, nimmt das Argument EmbedTrueTypeFonts den Wert der ** [EmbedTrueTypeFonts](ac8fb6a1-584a-2ddb-4216-53e30473ff65.md)** -Eigenschaft an.|
| _SaveNativePictureFormat_|Optional|**Variant**|Wenn Grafiken von einer anderen Plattform (z. B. Macintosh) importiert wurden, wird mit  **True** nur die Microsoft Windows-Version der importierten Grafiken gespeichert.|
| _SaveFormsData_|Optional|**Variant**|**True**, wenn die von einem Benutzer in ein Formular eingegebenen Daten als Datensatz gespeichert werden sollen.|
| _SaveAsAOCELetter_|Optional|**Variant**|Wenn mit dem Dokument ein Adressfeld verbunden ist, wird das Dokument mit  **True** als AOCE-Brief gespeichert (das Adressfeld wird gespeichert).|
| _Encoding_|Optional|**Variant**|Die Codepage oder der Zeichensatz für Dokumente, die als codierte Textdateien gespeichert wurden. Standardmäßig ist die Codepage des Systems festgelegt. Mit diesem Parameter können Sie nicht jede  ** [MsoEncoding](http://msdn.microsoft.com/library/286bed6e-6028-a252-5e4f-b505234d9d34%28Office.15%29.aspx)** -Konstante verwenden.|
| _InsertLineBreaks_|Optional|**Variant**|Wird das Dokument als Textdatei gespeichert, werden mit  **True** Zeilenumbrüche am Ende jeder Textzeile eingefügt.|
| _AllowSubstitutions_|Optional|**Variant**|Wenn das Dokument als Textdatei gespeichert wurde, ermöglicht  **True** das Ersetzen einiger Zeichen mit ähnlich aussehendem Text in Word. Beispielsweise wird das Copyrightzeichen als (c) angezeigt. Der Standardwert ist **False**.|
| _LineEnding_|Optional|**Variant**|Die Art, wie in Word Zeilen- und Absatzumbrüche in Dokumenten markiert werden, die als Textdateien gespeichert wurden. Dies kann eine der folgenden  ** [WdLineEndingType](6a0d8e41-4248-e48f-1db6-77fa59a6f212.md)** -Konstanten sein: **wdCRLF** (Standardwert) oder **wdCROnly**.|
| _AddBiDiMarks_|Optional|**Variant**|Mit  **True** werden Steuerzeichen zur Ausgabedatei hinzugefügt, um das bidirektionale Layout des Texts im ursprünglichen Dokument beizubehalten.|
| _CompatibilityMode_|Optional|**Variant**|Der Kompatibilitätsmodus, der beim Öffnen des Dokuments von Word verwendet wird.  ** [WdCompatibilityMode](244e1519-32ea-741c-7e2a-ff8a2e0713aa.md)** -Konstante. <BR/><BR/>**Wichtig**<BR/>  Wenn für diesen Wert kein Parameter angegeben ist, gibt Word standardmäßig einen Wert von 0 ein, was angibt, dass der aktuelle Kompatibilitätsmodus des Dokuments beibehalten werden sollte. |

### Rückgabewert

Nothing
 

 

## Beispiel

Im folgenden Codebeispiel wird das aktive Dokument als Test.rtf im RTF-Format (Rich Text Format) gespeichert.
 

 

```
Sub SaveAsRTF() 
    ActiveDocument.SaveAs2 FileName:="Text.rtf", _ 
        FileFormat:=wdFormatRTF 
End Sub
```

Im folgenden Codebeispiel wird das aktive Dokument im Textdateiformat mit der Erweiterung TXT gespeichert.
 

 



```
Sub SaveAsTextFile() 
    Dim strDocName As String 
    Dim intPos As Integer 
 
    ' Find position of extension in file name 
    strDocName = ActiveDocument.Name 
    intPos = InStrRev(strDocName, ".") 
 
    If intPos = 0 Then 
 
        ' If the document has not yet been saved 
        ' Ask the user to provide a file name 
        strDocName = InputBox("Please enter the name " &amp; _ 
            "of your document.") 
    Else 
 
        ' Strip off extension and add ".txt" extension 
        strDocName = Left(strDocName, intPos - 1) 
        strDocName = strDocName &amp; ".txt" 
    End If 
 
    ' Save file with new extension 
    ActiveDocument.SaveAs2 FileName:=strDocName, _ 
        FileFormat:=wdFormatText 
End Sub
```

Im folgenden Codebeispiel werden alle installierten Konverter durchlaufen. Wird der WordPerfect 6.0-Konverter gefunden, wird das aktive Dokument mit diesem Konverter gespeichert.
 

 



```
Sub SaveWithConverter() 
 
    Dim cnvWrdPrf As FileConverter 
 
    ' Look for WordPerfect file converter 
    ' And save document using the converter 
    ' For the FileFormat converter value 
    For Each cnvWrdPrf In Application.FileConverters 
        If cnvWrdPrf.ClassName = "WrdPrfctWin" Then 
            ActiveDocument.SaveAs2 FileName:="MyWP.doc", _ 
                FileFormat:=cnvWrdPrf.SaveFormat 
        End If 
    Next cnvWrdPrf 
 
End Sub
```

Im folgenden Codebeispiel wird eine Prozedur gezeigt, die ein Dokument mit einem Kennwort speichert.
 

 



```
Sub SaveWithPassword(docCurrent As Document, strPWD As String) 
    With docCurrent 
        .SaveAs2 WritePassword:=strPWD 
    End With 
End Sub
```


## Siehe auch


#### Konzepte


 
 [Document-Objekt](8d83487a-2345-a036-a916-971c9db5b7fb.md)
#### Weitere Ressourcen


 
 [Document-Objektmember](fc9ab457-0888-f917-3d52-387168ac23b9.md)