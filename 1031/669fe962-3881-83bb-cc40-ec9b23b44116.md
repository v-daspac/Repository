

# Application.Nz-Methode (Access)

Sie können die  **Nz** -Funktion verwenden, um Null, eine leere Zeichenfolge (" ") oder einen anderen angegebenen Wert zurückzugeben, wenn eine Variable des Typs **Variant** den Wert **Null** aufweist. Sie können diese Funktion z. B. verwenden, um einen Wert **Null** in einen anderen Wert zu konvertieren und so zu verhindern, dass der Wert durch einen Ausdruck verbreitet wird.
 


## Syntax

 *Ausdruck*  . **Nz**( ** *Value* **, ** *ValueIfNull* ** )
 

 
 *Ausdruck*  Eine Variable, die ein **Application** -Objekt darstellt.
 

 

### Parameter



|**Name**|**Erforderlich/Optional**|**Datentyp**|**Beschreibung**|
|:-----|:-----|:-----|:-----|
| _Value_|Erforderlich|**Variant**|Eine Variable des Datentyps  **Variant**.|
| _ValueIfNull_|Optional|**Variant**|Optional (außer in einer Abfrage). Eine  **Variante**, die einen Wert liefert, der zurückgegeben werden soll, wenn das Variantenargument **Null** ist. Mithilfe dieses Arguments können Sie einen anderen Wert als 0 oder eine Zeichenfolge der Länge 0 zurückgeben. **Hinweis**  Wenn Sie die  **Nz** -Funktion in einem Ausdruck in einer Abfrage ohne das *valueifnull*  -Argument verwenden, sind die Ergebnisse eine Zeichenfolge mit der Länge 0 in den Feldern, die Null-Werte enthalten. |

### Rückgabewert

Variant
 

 

## Bemerkungen

Falls der Wert des Variant-Arguments  **Null** ist, gibt die **Nz** -Funktion die Zahl Null oder eine leere Zeichenfolge zurück (gibt immer eine leere Zeichenfolge zurück, wenn sie in einem Abfrageausdruck verwendet wird), je nachdem, ob der Kontext anzeigt, dass der Wert eine Zahl oder eine Zeichenfolge sein soll. Wenn das optionale valueifnull-Argument angegeben ist, gibt die **Nz** -Funktion den durch dieses Argument angegebenen Wert zurück, wenn das Variant-Argument den Wert **Null** aufweist. Bei Verwendung in einem Abfrageausdruck sollte die **Nz** -Funktion immer das valueifnull-Argument verwenden.
 

 
Wenn der Wert von "Variant" ungleich  **Null** ist, gibt die **Null** -Funktion den Wert von "Variant" zurück.
 

 
Die  **Nz** -Funktion ist für Ausdrücke sinnvoll, die **Null** -Werte enthalten können. Damit ein Ausdruck einen Wert liefert, der ungleich **Null** ist, auch wenn er einen **Null** -Wert enthält, verwenden Sie die **Nz** -Funktion, um eine Null, eine leere Zeichenfolge oder einen benutzerdefinierten Rückgabewert zurückzugeben.
 

 
Der Ausdruck  `2 + varX` gibt z. B. immer den Wert **Null** zurück, wenn die Variable `varX` mit **Null** belegt ist. Der Ausdruck `2 + Nz(varX)` gibt dagegen 2 zurück.
 

 
Sie können die  **Nz** -Funktion häufig als Alternative zur **IIf** -Funktion verwenden. So sind z. B. im folgenden Code zwei jeweils mit der **IIf** -Funktion formulierte Ausdrücke erforderlich, um das gewünschte Ergebnis zurückzugeben. Der erste Ausdruck mit der **IIf** -Funktion wird verwendet, um den Wert einer Variable zu überprüfen und, sofern dieser gleich **Null** ist, in 0 umzuwandeln.
 

 



```
varTemp = IIf(IsNull(varFreight), 0, varFreight) 
varResult = IIf(varTemp > 50, "High", "Low")
```

Im nächsten Beispiel erledigt die  **Nz** -Funktion die gleiche Aufgabe wie der erste Ausdruck, und das gewünschte Ergebnis wird in einem Schritt statt in zwei Schritten erreicht.
 

 



```
varResult = IIf(Nz(varFreight) > 50, "High", "Low")
```

Wenn Sie einen Wert für das optionale valueifnull-Argument angeben, wird dieser Wert zurückgegeben, wenn "Variant" den Wert  **Null** hat. Durch Verwendung dieses optionalen Arguments können Sie eventuell darauf verzichten, einen Ausdruck zu verwenden, der die **IIf** -Funktion enthält. Der folgende Ausdruck verwendet z. B. die **IIf** -Funktion, um eine Zeichenfolge zurückzugeben, falls der Wert von `varFreight` gleich **Null** ist.
 

 



```
varResult = IIf(IsNull(varFreight), "No Freight Charge", varFreight)
```

Im nächsten Beispiel liefert das an die  **Nz** -Funktion übergebene optionale Argument die Zeichenfolge, die zurückgegeben werden soll, falls `varFreight` gleich **Null** ist.
 

 



```
varResult = Nz(varFreight, "No Freight Charge")
```

 **Link zur Verfügung gestellt von:**
 
![Symbol für Communitymitglied](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png)
 
 [UtterAccess](http://www.utteraccess.com)-Community
 

 

-  [NULL-Werte und ihr Verhalten](http://www.utteraccess.com/wiki/index.php/Nulls_And_Their_Behavior)
    
 

## Beispiel

Das folgende Beispiel wertet ein Steuerelement eines Formulars aus und gibt eine von zwei Zeichenfolgen zurück, basierend auf dem Wert des Steuerelements. Sofern der Wert des Steuerelements gleich  **Null** ist, verwendet die Prozedur die **Nz** -Funktion, um den Wert **Null** in eine leere Zeichenfolge umzuwandeln.
 

 

```
Public Sub CheckValue() 
 
    Dim frm As Form 
    Dim ctl As Control 
    Dim varResult As Variant 
 
    ' Return Form object variable pointing to Orders form. 
    Set frm = Forms!Orders 
 
    ' Return Control object variable pointing to ShipRegion. 
    Set ctl = frm!ShipRegion 
 
    ' Choose result based on value of control. 
    varResult = IIf(Nz(ctl.Value) = vbNullString, _ 
        "No value.", "Value is " &amp; ctl.Value &amp; ".") 
 
    ' Display result. 
    MsgBox varResult, vbExclamation 
 
End Sub
```


## Informationen zu den Mitwirkenden
<a name="AboutContributors"> </a>

UtterAccess ist das führende Microsoft Access-Wiki und -Hilfeforum. Zum Beitreten hier klicken.
 

 

## Siehe auch
<a name="AboutContributors"> </a>


#### Konzepte


 
 [Application-Objekt](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Weitere Ressourcen


 
 [Application-Objektelemente](3ab5276c-d52a-72a9-244c-ec92ead48811.md)