
# Evento ComboBox.NotInList (Access)

O evento  **NotInList** ocorre quando o usuário insere um valor na parte da caixa de texto de uma caixa de combinação que não se encontra na lista da caixa de combinação.
 


## Sintaxe

 *expressão*  . **NotInList**( ** *NewData* **, ** *Response* ** )
 

 
 *expressão*  Uma variável que representa um objeto **ComboBox**.
 

 

### Parâmetros



|**Nome**|**Obrigatório/opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _NewData_|Obrigatório|**String**|Uma cadeia de caracteres que o Microsoft Access usa para passar para o procedimento de evento o texto que o usuário digitou na parte caixa de texto da caixa de combinação.|
| _Response_|Obrigatório|**Integer**| A configuração indica como o evento **NotInList** foi tratado. O argumento _Response_ pode ser uma das seguintes constantes intrínsecas: **acDataErrDisplay** (Padrão) Exibe a mensagem padrão para o usuário. Você poderá usá-la quando não desejar permitir que o usuário adicione um novo valor à lista da caixa de combinação. **acDataErrContinue** Não exibe a mensagem padrão para o usuário. Você poderá usá-la quando desejar exibir uma mensagem personalizada para o usuário. Por exemplo, o procedimento de evento pode exibir uma caixa de diálogo personalizada que pergunta se o usuário deseja salvar a nova entrada. Se a resposta for Sim, o procedimento de evento adicionará a nova entrada à lista e definirá o argumento _Response_ como **acDataErrAdded**. Se a resposta for Não, o procedimento de evento definirá o argumento _Response_ como **acDataErrContinue**. **acDataErrAdded** Não exibe uma mensagem para o usuário, mas permite adicionar a entrada à lista da caixa de combinação no procedimento de evento **NotInList**. Depois que a entrada é adicionada, o Microsoft Access atualiza a lista refazendo a consulta da caixa de combinação. O Microsoft Access volta então a verificar a cadeia de caracteres em relação à lista da caixa de combinação e salva o valor do argumento _NewData_ no campo ao qual a caixa de combinação está acoplada. Se a cadeia de caracteres não estiver na lista, o Microsoft Access exibirá uma mensagem de erro.|

## Comentários

Para executar uma macro ou um procedimento de evento quando esse evento ocorrer, defina a propriedade  ** [OnNotInList](307e9f0c-6db7-b995-166b-060c697b9f6e.md)** com o nome da macro ou como [Procedimento do Evento].
 

 
Esse evento permite que o usuário adicione um novo valor à lista da caixa de combinação.
 

 
A propriedade  ** [LimitToList](885ed814-6e04-b9f1-0acb-3ded28e00f93.md)** precisa estar definida como Sim para que o evento **NotInList** ocorra.
 

 
O evento  **NotInList** não dispara o evento **Error**.
 

 
O evento  **NotInList** ocorre para caixas de combinação cuja propriedade **LimitToList** está definida como Sim, depois de você inserir um valor que não está na lista e tentar ir para outro controle ou salvar o registro. O evento ocorre após todos os eventos **Change** da caixa de combinação.
 

 
Quando a propriedade  ** [AutoExpand](0b3fabf8-4004-0868-3ddc-aef297514324.md)** for definida como Sim, o Microsoft Access selecionará valores correspondentes na lista, conforme o usuário inserir os caracteres na parte da caixa de texto da caixa de combinação. Se os caracteres que o usuário digitar forem correspondentes com os primeiros caracteres de um valor na lista (por exemplo, o usuário digita "Smith" e "Smithson" é um valor na lista), o evento **NotInList** não ocorrerá quando o usuário for para outro controle ou salvar o registro. Contudo, os caracteres que o Microsoft Access adicionará aos caracteres que o usuário digitar (no exemplo, "son") serão selecionados na parte da caixa de texto da caixa de combinação. Se o usuário quiser que o evento **NotInList** seja disparado em tais casos (por exemplo, o usuário deseja adicionar o novo nome "Smith" à lista da caixa de combinação), ele poderá inserir um caractere **SPACE**,  **BACKSPACE** ou **DEL** após o último caractere no novo valor.
 

 
Quando a propriedade  **LimitToList** for definida como Sim e a lista da caixa de combinação estiver suspensa, o Microsoft Access selecionará valores correspondentes na lista conforme o usuário inserir caracteres na parte da caixa de texto da caixa de combinação, mesmo que a propriedade **AutoExpand** esteja definida como Não. Se o usuário pressionar **ENTER** ou ir para outro controle ou registro, o valor selecionado aparecerá na caixa de combinação. Nesse caso, o evento **NotInList** não será disparado. Para permitir que o evento **NotInList** seja disparado, o usuário não deverá suspender a lista da caixa de combinação.
 

 

## Exemplo

O exemplo a seguir usa o evento  **NotInList** para adicionar um item a uma caixa de combinação.
 

 
Para testar esse exemplo, crie uma caixa de combinação denominada Colors em um formulário. Defina a propriedade  **LimitToList** da caixa de combinação como Sim. Para popular a caixa de combinação, defina a propriedade **RowSourceType** dela como Value List e forneça uma lista de valores separados por ponto-e-vírgula como a configuração da propriedade **RowSource**. Por exemplo, é possível fornecer os seguintes valores como a configuração dessa propriedade: Red, Green e Blue.
 

 
Em seguida, adicione o seguinte procedimento de evento ao formulário. Mude para o modo Formulário e insira um novo valor na parte de texto da caixa de combinação.
 

 

 **Observação**  Esse exemplo adiciona um item a uma caixa de combinação desacoplada. Ao adicionar um item a uma caixa de combinação acoplada, você adiciona um valor a um campo na fonte de dados subjacente. Na maioria dos casos, não é possível simplesmente adicionar um campo em um registro ? novo. Dependendo da estrutura de dados na tabela, provavelmente, você precisará adicionar um ou mais campos para atender aos requisitos de dados. Por exemplo, um novo registro deve incluir valores para os campos que contiverem a chave primária. Se for preciso adicionar itens dinamicamente a uma caixa de combinação acoplada, solicite ao usuário que insira dados em todos os campos obrigatórios, salve o novo registro e repita a consulta da caixa de combinação para exibir o novo valor.
 




```
Private Sub Colors_NotInList(NewData As String, _ 
        Response As Integer) 
    Dim ctl As Control 
     
    ' Return Control object that points to combo box. 
    Set ctl = Me!Colors 
    ' Prompt user to verify they wish to add new value. 
    If MsgBox("Value is not in list. Add it?", _ 
         vbOKCancel) = vbOK Then 
        ' Set Response argument to indicate that data 
        ' is being added. 
        Response = acDataErrAdded 
        ' Add string in NewData argument to row source. 
        ctl.RowSource = ctl.RowSource &amp; ";" &amp; NewData 
    Else 
    ' If user chooses Cancel, suppress error message 
    ' and undo changes. 
        Response = acDataErrContinue 
        ctl.Undo 
    End If 
End Sub
```


 

 

 
O exemplo a seguir mostra como usar o evento  **NotInList** para adicionar um item a uma caixa de combinação acoplada.
 

 
 **Código de exemplo fornecido por:** Bill Jelen, [MrExcel.com](http://www.mrexcel.com/)
 

 



```
Private Sub cboDept_NotInList(NewData As String, Response As Integer)
    Dim oRS As DAO.Recordset, i As Integer, sMsg As String
    Dim oRSClone As DAO.Recordset

    Response = acDataErrContinue

    If MsgBox("Add dept?", vbYesNo) = vbYes Then
        Set oRS = CurrentDb.OpenRecordset("tblDepartments", dbOpenDynaset)
        oRS.AddNew
        oRS.Fields(1) = NewData
        For i = 2 To oRS.Fields.Count - 1
            sMsg = "What do you want for " &amp; oRS(i).Name
            oRS(i).Value = InputBox(sMsg, , oRS(i).DefaultValue)
        Next i
        oRS.Update
        cboDept = Null
        cboDept.Requery
        DoCmd.OpenTable "tblDepartments", acViewNormal, acReadOnly
        DoCmd.GoToRecord acDataTable, "tblDepartments", acLast
    End If
End Sub
```

O exemplo a seguir mostra como adicionar um item a uma caixa de combinação associada.
 

 
 **Código de exemplo fornecido por:** A [Referência do programador do Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)
 

 



```
Private Sub cboMainCategory_NotInList(NewData As String, Response As Integer)

    On Error GoTo Error_Handler
    Dim intAnswer As Integer
    intAnswer = MsgBox("""" &amp; NewData &amp; """ is not an approved category. " &amp; vbcrlf _
        &amp; "Do you want to add it now?" _ vbYesNo + vbQuestion, "Invalid Category")

    Select Case intAnswer
        Case vbYes
            DoCmd.SetWarnings False
            DoCmd.RunSQL "INSERT INTO tlkpCategoryNotInList (Category) "
                &amp; _ "Select """ &amp; NewData &amp; """;"
            DoCmd.SetWarnings True
            Response = acDataErrAdded
        Case vbNo
            MsgBox "Please select an item from the list.", _
                vbExclamation + vbOKOnly, "Invalid Entry"
            Response = acDataErrContinue

    End Select

    Exit_Procedure:
        DoCmd.SetWarnings True
        Exit Sub

    Error_Handler:
        MsgBox Err.Number &amp; ", " &amp; Error Description
        Resume Exit_Procedure
        Resume

End Sub
```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

Holy Macro! Livros publica livros de entretenimento para as pessoas que usam o Microsoft Office. Veja o catálogo completo em MrExcel.com.
 

 
Wrox Press é impulsionada pelo programador para programador filosofia. Livros Wrox são escritos por programadores para programadores, ea marca Wrox significa soluções autoritárias para problemas de programação do mundo real.
 

 

## Ver também
<a name="AboutContributors"> </a>


#### Conceitos


 
 [Objeto ComboBox](1cf508d5-023e-eb38-3991-71e82b2a4e7e.md)
#### Outros recursos


 
 [Membros do objeto ComboBox](d0d83ca3-3698-295e-5335-7d0816557d6b.md)