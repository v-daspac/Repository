

# Método Application.Nz (Access)

Use a função  **Nz** para retornar zero, uma cadeia de comprimento zero (" ") ou outro valor especificado quando uma **Variant** for **Null**. Por exemplo, essa função pode ser usada para converter um valor **Null** em outro valor e evitar que ele se propague por meio de uma expressão.
 


## Sintaxe

 *expressão*  . **Nz**( ** *Value* **, ** *ValueIfNull* ** )
 

 
 *expressão*  Uma variável que representa um objeto **Application**.
 

 

### Parâmetros



|**Nome**|**Obrigatório/opcional**|**Tipo de dados**|**Descrição**|
|:-----|:-----|:-----|:-----|
| _Value_|Obrigatório|**Variant**|Uma variável do tipo de dados **Variant**.|
| _ValueIfNull_|Opcional|**Variant**|Opcional (a menos que usado em uma consulta). Um  **Variant** que fornecerá um valor a ser retornado se o argumento variant for **Null**. Esse argumento permite que você retorne um valor diferente de zero ou uma cadeia de comprimento zero. **Observação**  Se você usar a função  **Nz** em uma expressão em uma consulta sem usar o argumento *valueifnull*  , os resultados serão uma cadeia de comprimento zero nos campos que contiverem valores nulos. |

### Valor de retorno

Variante
 

 

## Comentários

Se o valor do argumento variant for  **Null**, a função **Nz** retornará o número zero ou uma cadeia de comprimento zero (sempre retorna uma cadeia de comprimento zero quando usada em uma expressão de consulta), dependendo se o contexto indicar que o valor deve ser um número ou uma cadeia de caracteres. Se o argumento opcional valueifnull for incluído, a função **Nz** retornará o valor especificado por esse argumento se o argumento variant for **Null**. Quando usada em uma expressão de consulta, a função **Nz** deve incluir sempre o argumento valueifnull.
 

 
Se o valor de variant não for  **Null**, a função **Nz** retornará o valor de variant.
 

 
A função  **Nz** é útil em expressões que possam incluir valores **Null**. Para forçar uma expressão a ser avaliada como um valor diferente de **Null** mesmo quando contiver um valor **Null**, use a função **Nz** para retornar um zero, uma cadeia de comprimento zero ou um valor de retorno personalizado.
 

 
Por exemplo, a expressão  `2 + varX` sempre retorna um valor **Null** quando a **Variant** `varX` é **Null**. Entretanto, `2 + Nz(varX)` retorna 2.
 

 
Frequentemente a função  **Nz** é usada como uma alternativa para a função **IIf**. No código a seguir, por exemplo, são necessárias duas expressões que incluam a função **IIf** para retornar o resultado desejado. A primeira expressão incluindo a função **IIf** é usada para verificar o valor de uma variável e convertê-lo em zero se ele for **Null**.
 

 



```
varTemp = IIf(IsNull(varFreight), 0, varFreight) 
varResult = IIf(varTemp > 50, "High", "Low")
```

No próximo exemplo, a função  **Nz** proporciona a mesma funcionalidade da primeira expressão, e o resultado desejado é alcançado em uma etapa, em vez de duas.
 

 



```
varResult = IIf(Nz(varFreight) > 50, "High", "Low")
```

Se você fornecer um valor para o argumento opcional valueifnull, esse valor será retornado quando variant for  **Null**. Ao incluir esse argumento opcional, pode-se evitar o uso de uma expressão contendo a função **IIf**. Por exemplo, a expressão seguinte usará a função **IIf** para retornar uma cadeia de caracteres se o valor de `varFreight` for **Null**.
 

 



```
varResult = IIf(IsNull(varFreight), "No Freight Charge", varFreight)
```

No próximo exemplo, o argumento opcional fornecido para a função  **Nz** fornece a cadeia de caracteres a ser retornada caso `varFreight` seja **Null**.
 

 



```
varResult = Nz(varFreight, "No Freight Charge")
```

 **Link fornecido por:**
 
![Ícone de Membro da Comunidade](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png)
 
 a comunidade do [UtterAccess](http://www.utteraccess.com)
 

 

-  [Valores nulos e seu comportamento](http://www.utteraccess.com/wiki/index.php/Nulls_And_Their_Behavior)
    
 

## Exemplo

O exemplo a seguir avalia um controle em um formulário e retorna uma entre duas cadeias de caracteres com base no valor do controle. Quando o valor do controle for  **Null**, o procedimento usará a função **Nz** para converter o valor **Null** em uma cadeia de caracteres de comprimento zero.
 

 

```
Public Sub CheckValue() 
 
    Dim frm As Form 
    Dim ctl As Control 
    Dim varResult As Variant 
 
    ' Return Form object variable pointing to Orders form. 
    Set frm = Forms!Orders 
 
    ' Return Control object variable pointing to ShipRegion. 
    Set ctl = frm!ShipRegion 
 
    ' Choose result based on value of control. 
    varResult = IIf(Nz(ctl.Value) = vbNullString, _ 
        "No value.", "Value is " &amp; ctl.Value &amp; ".") 
 
    ' Display result. 
    MsgBox varResult, vbExclamation 
 
End Sub
```


## Sobre os colaboradores
<a name="AboutContributors"> </a>

UtterAccess é o fórum principal de wiki e a Ajuda do Microsoft Access. clique aqui para ingressar.
 

 

## Ver também
<a name="AboutContributors"> </a>


#### Conceitos


 
 [Objeto Application](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Outros recursos


 
 [Membros do objeto Application](3ab5276c-d52a-72a9-244c-ec92ead48811.md)