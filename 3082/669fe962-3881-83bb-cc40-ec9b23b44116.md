

# Método Application.Nz (Access)

Puede usar la función  **Nz** para devolver cero, una cadena de longitud cero ("") o cualquier otro valor especificado cuando un tipo de datos **Variant** es **Null**. Por ejemplo, se puede usar esta función para convertir un valor **Null** en otro valor y evitar que se propague a través de una expresión.
 


## Sintaxis

 *expresión*  . **Nz**( ** *Value* **, ** *ValueIfNull* ** )
 

 
 *expresión*  Variable que representa un objeto **Application**.
 

 

### Parámetros



|**Nombre**|**Necesario/Opcional**|**Tipo de datos**|**Descripción**|
|:-----|:-----|:-----|:-----|
| _Value_|Obligatorio|**Variant**|Variable del tipo de datos  **Variant**.|
| _ValueIfNull_|Opcional|**Variant**|Opcional (a menos que se use en una consulta). Un tipo  **Variant** que proporciona el valor que se devolverá si el argumento variant es **Null**. Este argumento permite devolver un valor que no sea cero ni una cadena de longitud cero. **Nota**  Si usa la función  **Nz** en una expresión de una consulta sin usar el argumento *valueifnull*  , el resultado será una cadena de longitud cero en los campos que contengan valores nulos. |

### Valor devuelto

Variante
 

 

## Observaciones

Si el valor del argumento variante es  **Null**, la función **Nz** devuelve el número cero o una cadena de longitud cero (siempre devuelve una cadena de longitud cero cuando se usa en una expresión de consulta), dependiendo de si el contexto indica que el valor debe ser un número o una cadena. Si se incluye el argumento opcional valueifnull, la función **Nz** devolverá el valor especificado en ese argumento si el argumento variant es **Null**. Cuando se usa en una expresión de consulta, la función **Nz** siempre debe incluir el argumento valueifnull.
 

 
Si el valor de variant no es  **Null**, la función **Nz** devuelve el valor de variant.
 

 
La función  **Nz** es útil para las expresiones que pueden incluir valores **Null**. Para forzar que una expresión tenga un valor que no sea **Null** aunque contenga un valor **Null**, use la función **Nz** para que se devuelva cero, una cadena de longitud cero o un valor personalizado.
 

 
Por ejemplo, la expresión  `2 + varX` siempre devolverá un valor **Null** cuando **Variant** `varX` sea **Null**. Sin embargo, `2 + Nz(varX)` devuelve 2.
 

 
La función  **Nz** se puede usar a menudo como alternativa a la función **IIf**. Por ejemplo, en el código siguiente, son necesarias dos expresiones que contengan la función **IIf** para que se devuelva el resultado deseado. La primera expresión con la función **IIf** se usa para comprobar el valor de una variable y convertirlo en cero si es **Null**.
 

 



```
varTemp = IIf(IsNull(varFreight), 0, varFreight) 
varResult = IIf(varTemp > 50, "High", "Low")
```

En el siguiente ejemplo, la función  **Nz** ofrece la misma funcionalidad que la primera expresión y el resultado deseado se obtiene en un paso en vez de dos.
 

 



```
varResult = IIf(Nz(varFreight) > 50, "High", "Low")
```

Si se proporciona un valor para el argumento opcional valueifnull, este valor se devolverá cuando variant sea  **Null**. Si se incluye este argumento opcional, puede que no haya que usar una expresión que contenga la función **IIf**. Por ejemplo, la expresión siguiente usa la función **IIf** para devolver una cadena si el valor de `varFreight` es **Null**.
 

 



```
varResult = IIf(IsNull(varFreight), "No Freight Charge", varFreight)
```

En el siguiente ejemplo, el argumento opcional de la función  **Nz** proporciona la cadena que se va a devolver si `varFreight` es **Null**.
 

 



```
varResult = Nz(varFreight, "No Freight Charge")
```

 **Vínculo proporcionado por:**
 
![Icono Miembro de la comunidad](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png)
 
 La comunidad de [UtterAccess](http://www.utteraccess.com)
 

 

-  [Los valores NULL y su comportamiento ](http://www.utteraccess.com/wiki/index.php/Nulls_And_Their_Behavior)
    
 

## Ejemplo

En el siguiente ejemplo se evalúa un control ubicado en un formulario y se devuelve una de las dos cadenas según el valor del control. Si el valor del control es  **Null**, el procedimiento usa la función **Nz** para convertir un valor **Null** en una cadena de longitud cero.
 

 

```
Public Sub CheckValue() 
 
    Dim frm As Form 
    Dim ctl As Control 
    Dim varResult As Variant 
 
    ' Return Form object variable pointing to Orders form. 
    Set frm = Forms!Orders 
 
    ' Return Control object variable pointing to ShipRegion. 
    Set ctl = frm!ShipRegion 
 
    ' Choose result based on value of control. 
    varResult = IIf(Nz(ctl.Value) = vbNullString, _ 
        "No value.", "Value is " &amp; ctl.Value &amp; ".") 
 
    ' Display result. 
    MsgBox varResult, vbExclamation 
 
End Sub
```


## Acerca de los colaboradores
<a name="AboutContributors"> </a>

UtterAccess es el principal foro de ayuda y wiki sobre Microsoft Access. Haga clic aquí para unirse.
 

 

## Vea también
<a name="AboutContributors"> </a>


#### Conceptos


 
 [Objeto Application](aefb0713-97e6-e2c7-e530-8fd2e1316a55.md)
#### Otros recursos


 
 [Miembros del objeto Application](3ab5276c-d52a-72a9-244c-ec92ead48811.md)