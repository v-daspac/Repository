
# Evento ComboBox.NotInList (Access)

El evento  **NotInList** se produce cuando el usuario escribe en el cuadro de texto de un cuadro combinado un valor que no se encuentra en la lista del cuadro combinado.
 


## Sintaxis

 *expresión*  . **NotInList**( ** *NewData* **, ** *Response* ** )
 

 
 *expresión*  Variable que representa un objeto **ComboBox**.
 

 

### Parámetros



|**Nombre**|**Obligatorio/opcional**|**Tipo de datos**|**Descripción**|
|:-----|:-----|:-----|:-----|
| _NewData_|Obligatorio|**Cadena**|Cadena que Microsoft Access utiliza para pasar al procedimiento de evento el texto especificado por el usuario en el cuadro de texto del cuadro combinado.|
| _Response_|Obligatorio|**Integer**| El valor indica cómo se controló el evento **NotInList**. El argumento _Response_ puede ser una de las siguientes constantes intrínsecas: **acDataErrDisplay** (valor predeterminado): muestra el mensaje predeterminado al usuario. Puede usarla cuando no quiera permitir al usuario que agregue un nuevo valor a la lista del cuadro combinado. **acDataErrContinue**: no muestra el mensaje predeterminado al usuario. Puede usarla cuando quiera mostrar un mensaje personalizado al usuario. Por ejemplo, el procedimiento de evento puede mostrar un cuadro de diálogo personalizado en el que se pregunta si el usuario quiere guardar la nueva entrada. Si la respuesta es Sí, el procedimiento de evento agregará la nueva entrada a la lista y establecerá el argumento _Response_ en **acDataErrAdded**. Si la respuesta es No, el procedimiento de evento establecerá el argumento _Response_ en **acDataErrContinue**. **acDataErrAdded**: no muestra ningún mensaje al usuario pero permite agregar la entrada a la lista del cuadro combinado en el procedimiento de evento **NotInList**. Tras agregar la entrada, Microsoft Access actualiza la lista consultando de nuevo el cuadro combinado. A continuación, Microsoft Access vuelve a comprobar la cadena mirando en la lista del cuadro combinado y guarda el valor en el argumento _NewData_ del campo al que está enlazado el cuadro combinado. Si la cadena no está en la lista, Microsoft Access mostrará un mensaje de error.|

## Observaciones

Para ejecutar una macro o procedimiento de evento cuando se produzca este evento, establezca la propiedad  ** [OnNotInList](307e9f0c-6db7-b995-166b-060c697b9f6e.md)** en el nombre de la macro o en [Procedimiento de evento].
 

 
Este evento permite al usuario agregar un nuevo valor a la lista del cuadro combinado.
 

 
Para que se produzca el evento  **NotInList**, es necesario que la propiedad ** [LimitToList](885ed814-6e04-b9f1-0acb-3ded28e00f93.md)** tenga el valor Sí.
 

 
El evento  **NotInList** no activa el evento **Error**.
 

 
El evento  **NotInList** se produce para los cuadros combinados cuya propiedad **LimitToList** tiene el valor Sí después de especificar un valor no incluido en la lista e intentar pasar a otro control o guardar el registro. El evento ocurre después de todos los eventos **Change** del cuadro combinado.
 

 
Cuando la propiedad  ** [AutoExpand](0b3fabf8-4004-0868-3ddc-aef297514324.md)** tiene el valor Sí, Microsoft Access selecciona los valores coincidentes en la lista cuando el usuario escribe caracteres en el cuadro de texto del cuadro combinado. Si los caracteres que escribe el usuario coinciden con los primeros caracteres de un valor de la lista (por ejemplo, el usuario escribe "Martín" y "Martínez" es un valor de la lista), el evento **NotInList** no se producirá cuando el usuario se mueva a otro control o guarde el registro. Sin embargo, los caracteres que Microsoft Access agrega a los caracteres que escribe el usuario (en el ejemplo, "ez") se seleccionan en el cuadro de texto del cuadro combinado. Si el usuario quiere que se produzca el evento **NotInList** en dichos casos (por ejemplo, si quiere agregar el nuevo apellido "Martín" a la lista del cuadro combinado), puede especificar un carácter **BARRA ESPACIADORA**,  **RETROCESO** o **SUPR** después del último carácter en el nuevo valor.
 

 
Cuando la propiedad  **LimitToList** está establecida en Sí y la lista del cuadro combinado está desplegada, Microsoft Access selecciona los valores coincidentes en la lista cuando el usuario escribe caracteres en la parte de cuadro de texto del cuadro combinado aunque la propiedad **AutoExpand** esté establecida en No. Si el usuario presiona **ENTRAR** o se desplaza a otro control o registro, el valor seleccionado aparece en el cuadro combinado. En este caso, el evento **NotInList** no se producirá. Para que el evento **NotInList** se produzca, el usuario no debe desplegar la lista del cuadro combinado.
 

 

## Ejemplo

En el ejemplo siguiente se usa el evento  **NotInList** para agregar un elemento a un cuadro combinado.
 

 
Para probar este ejemplo, cree un cuadro combinado denominado Colors (Colores) en un formulario. Establezca en Sí la propiedad  **LimitToList** del cuadro combinado. Para rellenar el cuadro combinado, establezca en Lista de valores la propiedad **RowSourceType** del cuadro combinado y proporcione una lista de valores separados por caracteres de punto y coma en el valor de la propiedad **RowSource**. Por ejemplo, puede proporcionar los siguientes valores para esta propiedad: Red; Green; Blue (Rojo; Verde; Azul).
 

 
A continuación, agregue el siguiente procedimiento de evento al formulario. Cambie a la vista Formulario y especifique un nuevo valor en la parte del cuadro combinado correspondiente al texto.
 

 

 **Nota**  En este ejemplo se agrega un elemento a un cuadro combinado independiente. Cuando se agrega un elemento a un cuadro combinado dependiente, se agrega un valor a un campo en el origen de datos subyacente. En la mayoría de los casos, no se puede agregar un campo en un nuevo registro. Dependiendo de la estructura de los datos en la tabla, es probable que tenga que agregar uno o varios campos para cumplir los requisitos de datos. Por ejemplo, un nuevo registro debe incluir valores para todos los campos que incluyan la clave principal. Si necesita agregar elementos a un cuadro combinado dependiente de forma dinámica, deberá solicitar al usuario que escriba datos en todos los campos requeridos, guardar el nuevo registro y luego volver a consultar el cuadro combinado para que aparezca el nuevo valor.
 




```
Private Sub Colors_NotInList(NewData As String, _ 
        Response As Integer) 
    Dim ctl As Control 
     
    ' Return Control object that points to combo box. 
    Set ctl = Me!Colors 
    ' Prompt user to verify they wish to add new value. 
    If MsgBox("Value is not in list. Add it?", _ 
         vbOKCancel) = vbOK Then 
        ' Set Response argument to indicate that data 
        ' is being added. 
        Response = acDataErrAdded 
        ' Add string in NewData argument to row source. 
        ctl.RowSource = ctl.RowSource &amp; ";" &amp; NewData 
    Else 
    ' If user chooses Cancel, suppress error message 
    ' and undo changes. 
        Response = acDataErrContinue 
        ctl.Undo 
    End If 
End Sub
```


 

 

 
En el ejemplo siguiente se ve cómo usar el evento  **NotInList** para agregar un elemento a un cuadro combinado dependiente.
 

 
 **Código de ejemplo proporcionado por:** Bill Jelen, [MrExcel.com ](http://www.mrexcel.com/)
 

 



```
Private Sub cboDept_NotInList(NewData As String, Response As Integer)
    Dim oRS As DAO.Recordset, i As Integer, sMsg As String
    Dim oRSClone As DAO.Recordset

    Response = acDataErrContinue

    If MsgBox("Add dept?", vbYesNo) = vbYes Then
        Set oRS = CurrentDb.OpenRecordset("tblDepartments", dbOpenDynaset)
        oRS.AddNew
        oRS.Fields(1) = NewData
        For i = 2 To oRS.Fields.Count - 1
            sMsg = "What do you want for " &amp; oRS(i).Name
            oRS(i).Value = InputBox(sMsg, , oRS(i).DefaultValue)
        Next i
        oRS.Update
        cboDept = Null
        cboDept.Requery
        DoCmd.OpenTable "tblDepartments", acViewNormal, acReadOnly
        DoCmd.GoToRecord acDataTable, "tblDepartments", acLast
    End If
End Sub
```

En el ejemplo siguiente se muestra cómo agregar un elemento a un cuadro combinado dependiente.
 

 
 **Código de ejemplo proporcionado por:** La [Referencia del programador de Microsoft Access 2010](http://www.wrox.com/WileyCDA/WroxTitle/Access-2010-Programmer-s-Reference.productCd-0470591668.mdl)
 

 



```
Private Sub cboMainCategory_NotInList(NewData As String, Response As Integer)

    On Error GoTo Error_Handler
    Dim intAnswer As Integer
    intAnswer = MsgBox("""" &amp; NewData &amp; """ is not an approved category. " &amp; vbcrlf _
        &amp; "Do you want to add it now?" _ vbYesNo + vbQuestion, "Invalid Category")

    Select Case intAnswer
        Case vbYes
            DoCmd.SetWarnings False
            DoCmd.RunSQL "INSERT INTO tlkpCategoryNotInList (Category) "
                &amp; _ "Select """ &amp; NewData &amp; """;"
            DoCmd.SetWarnings True
            Response = acDataErrAdded
        Case vbNo
            MsgBox "Please select an item from the list.", _
                vbExclamation + vbOKOnly, "Invalid Entry"
            Response = acDataErrContinue

    End Select

    Exit_Procedure:
        DoCmd.SetWarnings True
        Exit Sub

    Error_Handler:
        MsgBox Err.Number &amp; ", " &amp; Error Description
        Resume Exit_Procedure
        Resume

End Sub
```


## Acerca de los colaboradores
<a name="AboutContributors"> </a>

Holy Macro! Books publica libros de entretenimiento para personas que usan Microsoft Office. Puede consultar el catálogo completo en MrExcel.com.
 

 
Wrox Press se rige por la filosofía "de programador a programador". Los libros de Wrox están escritos por programadores para programadores y la marca Wrox es sinónimo de soluciones autorizadas para problemas reales de programación.
 

 

## Vea también
<a name="AboutContributors"> </a>


#### Conceptos


 
 [Objeto ComboBox](1cf508d5-023e-eb38-3991-71e82b2a4e7e.md)
#### Otros recursos


 
 [Miembros del objeto ComboBox](d0d83ca3-3698-295e-5335-7d0816557d6b.md)