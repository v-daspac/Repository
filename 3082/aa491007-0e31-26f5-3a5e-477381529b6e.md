

# Método Document.SaveAs2 (Word)

Guarda el documento especificado con un nuevo nombre o formato. Algunos argumentos de este método se corresponden con las opciones del cuadro de diálogo  **Guardar como** (pestaña **Archivo**).
 


## Sintaxis

 *expresión*  . **SaveAs2**( ** *FileName* **, ** *FileFormat* **, ** *LockComments* **, ** *Password* **, ** *AddToRecentFiles* **, ** *WritePassword* **, ** *ReadOnlyRecommended* **, ** *EmbedTrueTypeFonts* **, ** *SaveNativePictureFormat* **, ** *SaveFormsData* **, ** *SaveAsAOCELetter* **, ** *Encoding* **, ** *InsertLineBreaks* **, ** *AllowSubstitutions* **, ** *LineEnding* **, ** *AddBiDiMarks* **, ** *CompatibilityMode* ** )
 

 
 *expresión*  Expresión que devuelve el objeto ** [Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)**.
 

 

### Parámetros



|**Nombre**|**Necesario/Opcional**|**Tipo de datos**|**Descripción**|
|:-----|:-----|:-----|:-----|
| _FileName_|Opcional|**Variant**|Nombre del documento. Los valores predeterminados son la carpeta y el nombre del archivo actuales. Si el documento no se ha guardado nunca, se utiliza el nombre predeterminado, por ejemplo Doc1.doc. Si ya hay un documento con el nombre de archivo especificado, el documento se sobrescribe sin solicitar confirmación al usuario.|
| _FileFormat_|Opcional|**Variant**|Formato en que se guarda el documento. Puede ser cualquier constante  ** [WdSaveFormat](eb8d5689-9b4b-8d09-321c-1a3c1ee777e9.md)**. Para guardar un documento en otro formato, especifique el valor adecuado para la propiedad ** [SaveFormat](d837cd22-38eb-5160-1f85-16001448213e.md)** del objeto ** [FileConverter](41af2a9b-75cc-253d-4954-4fb42c88530f.md)**.|
| _LockComments_|Opcional|**Variant**|**True** para bloquear el documento contra comentarios. El valor predeterminado es **False**.|
| _Password_|Opcional|**Variant**|Cadena de contraseña para abrir el documento (vea el apartado Comentarios más adelante).|
| _AddToRecentFiles_|Opcional|**Variant**|**True** para agregar el documento a la lista de archivos usados recientemente en el menú **Archivo**. El valor predeterminado es  **True**.|
| _WritePassword_|Opcional|**Variant**|Cadena de contraseña para guardar los cambios del documento (vea el apartado Comentarios más adelante).|
| _ReadOnlyRecommended_|Opcional|**Variant**|**True** para que Microsoft Word sugiera el estado de solo lectura cada vez que se abra el documento. El valor predeterminado es **False**.|
| _EmbedTrueTypeFonts_|Opcional|**Variant**|**True** para guardar las fuentes TrueType con el documento. Si se omite, el argumento EmbedTrueTypeFonts presupone el valor de la propiedad ** [EmbedTrueTypeFonts](ac8fb6a1-584a-2ddb-4216-53e30473ff65.md)**.|
| _SaveNativePictureFormat_|Opcional|**Variant**|Si se importaron gráficos desde otra plataforma (por ejemplo, Macintosh),  **True** para guardar solo la versión de Microsoft Windows de los gráficos importados.|
| _SaveFormsData_|Opcional|**Variant**|**True** para guardar como registro los datos que especifique un usuario en un formulario.|
| _SaveAsAOCELetter_|Opcional|**Variant**|Si el documento tiene un formulario adjunto,  **True** para guardar el documento como una carta AOCE (se guarda el formulario).|
| _Encoding_|Opcional|**Variant**|Página de código o conjunto de caracteres que se debe usar para los documentos que se guardan como archivos de texto codificado. El valor predeterminado es la página de código del sistema. No puede usar todas las constantes  ** [MsoEncoding](http://msdn.microsoft.com/library/286bed6e-6028-a252-5e4f-b505234d9d34%28Office.15%29.aspx)** con este parámetro.|
| _InsertLineBreaks_|Opcional|**Variant**|Si el documento se guarda como archivo de texto,  **True** para insertar saltos de línea al final de cada línea de texto.|
| _AllowSubstitutions_|Opcional|**Variant**|Si el documento se guarda como archivo de texto,  **True** permite a Word reemplazar algunos símbolos con texto de aspecto similar. Por ejemplo, mostrar el símbolo de copyright como (c). El valor predeterminado es **False**.|
| _LineEnding_|Opcional|**Variant**|La manera en que Word marca los saltos de línea y párrafo de los documentos que se guardan como archivos de texto. Puede ser una de las constantes  ** [WdLineEndingType](6a0d8e41-4248-e48f-1db6-77fa59a6f212.md)** siguientes: **wdCRLF** (valor predeterminado) o **wdCROnly**.|
| _AddBiDiMarks_|Opcional|**Variant**|**True** agrega caracteres de control al archivo de salida para conservar el diseño bidireccional del texto del documento original.|
| _CompatibilityMode_|Opcional|**Variant**|Modo de compatibilidad que Word usa al abrir el documento. La constante  ** [WdCompatibilityMode](244e1519-32ea-741c-7e2a-ff8a2e0713aa.md)**. **Importante**  De manera predeterminada, si no se especifica ningún valor para este parámetro, Word indica un valor de 0, que especifica que se debe conservar el modo de compatibilidad actual del documento. |

### Valor devuelto

Nothing
 

 

## Ejemplo

En el código de ejemplo siguiente, el documento activo se guarda como Test.rtf en formato de texto enriquecido (RTF).
 

 

```
Sub SaveAsRTF() 
    ActiveDocument.SaveAs2 FileName:="Text.rtf", _ 
        FileFormat:=wdFormatRTF 
End Sub
```

En el código de ejemplo siguiente, el documento activo se guarda en el formato de archivo de texto con la extensión ".txt".
 

 



```
Sub SaveAsTextFile() 
    Dim strDocName As String 
    Dim intPos As Integer 
 
    ' Find position of extension in file name 
    strDocName = ActiveDocument.Name 
    intPos = InStrRev(strDocName, ".") 
 
    If intPos = 0 Then 
 
        ' If the document has not yet been saved 
        ' Ask the user to provide a file name 
        strDocName = InputBox("Please enter the name " &amp; _ 
            "of your document.") 
    Else 
 
        ' Strip off extension and add ".txt" extension 
        strDocName = Left(strDocName, intPos - 1) 
        strDocName = strDocName &amp; ".txt" 
    End If 
 
    ' Save file with new extension 
    ActiveDocument.SaveAs2 FileName:=strDocName, _ 
        FileFormat:=wdFormatText 
End Sub
```

En el código de ejemplo siguiente, se hace un bucle de todos los convertidores instalados y, si se encuentra el convertidor de WordPerfect 6.0, guarda el documento activo mediante el convertidor.
 

 



```
Sub SaveWithConverter() 
 
    Dim cnvWrdPrf As FileConverter 
 
    ' Look for WordPerfect file converter 
    ' And save document using the converter 
    ' For the FileFormat converter value 
    For Each cnvWrdPrf In Application.FileConverters 
        If cnvWrdPrf.ClassName = "WrdPrfctWin" Then 
            ActiveDocument.SaveAs2 FileName:="MyWP.doc", _ 
                FileFormat:=cnvWrdPrf.SaveFormat 
        End If 
    Next cnvWrdPrf 
 
End Sub
```

En el código de ejemplo siguiente, se muestra un procedimiento que guarda un documento con una contraseña.
 

 



```
Sub SaveWithPassword(docCurrent As Document, strPWD As String) 
    With docCurrent 
        .SaveAs2 WritePassword:=strPWD 
    End With 
End Sub
```


## Vea también


#### Conceptos


 
 [Objeto Document](8d83487a-2345-a036-a916-971c9db5b7fb.md)
#### Otros recursos


 
 [Miembros del objeto Document](fc9ab457-0888-f917-3d52-387168ac23b9.md)