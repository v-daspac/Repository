
# Evento Form.Error (Access)

El evento Error se produce si tiene lugar un error en tiempo de ejecución en Microsoft Access cuando un formulario tiene el enfoque.
 


## Sintaxis

 *expresión*  . **Error**( ** *DataErr* **, ** *Response* ** )
 

 
 *expresión*  Variable que representa un objeto **Form**.
 

 

### Parámetros



|**Nombre**|**Obligatorio/opcional**|**Tipo de datos**|**Descripción**|
|:-----|:-----|:-----|:-----|
| _DataErr_|Obligatorio|**Integer**|Código de error devuelto por el objeto Err cuando se produce un error. Puede usar el argumento DataErr con la función Error para asignar el número al mensaje de error correspondiente.|
| _Response_|Obligatorio|**Integer**| La configuración determina si se muestra o no un mensaje de error. El argumento Response puede ser una de las siguientes constantes intrínsecas. **acDataErrContinue** Se omite el error y se continúa sin mostrar el mensaje de error de Microsoft Access predeterminado. Puede proporcionar un mensaje de error personalizado en lugar del predeterminado. **acDataErrDisplay** (valor predeterminado) Muestra el mensaje de error de Microsoft Access predeterminado.|

## Observaciones

Esto incluye los errores del motor de base de datos de Microsoft Access, pero no los errores en tiempo de ejecución de Visual Basic ni los errores de ADO.
 

 
Para ejecutar una macro o procedimiento de evento cuando se produzca este evento, establezca la propiedad  **OnError** en el nombre de la macro o en [Procedimiento de evento].
 

 
Al ejecutar un procedimiento de evento o una macro cuando se produce un evento Error, puede interceptar un mensaje de error de Microsoft Access y mostrar un mensaje personalizado que tenga un significado más acorde con la aplicación.
 

 
 **Vínculo proporcionado por:**
 
![Icono Miembro de la comunidad](images/8b9774c4-6c97-470e-b3a2-56d8f786444c.png)
 
 La comunidad de [UtterAccess](http://www.utteraccess.com)
 

 

- Errores
    
 

## Ejemplo

En el siguiente ejemplo, se muestra cómo se puede reemplazar el mensaje de error predeterminado por otro personalizado. Cuando Microsoft Access devuelve un mensaje de error en el que se indica que se ha detectado una clave duplicada (código de error 3022), este procedimiento de evento muestra un mensaje que facilita a los usuarios más información específica de la aplicación.
 

 
Para probar el ejemplo, agregue el siguiente procedimiento de evento a un formulario basado en una tabla con un Id. de empleado único como clave de cada registro.
 

 



```
Private Sub Form_Error(DataErr As Integer, Response As Integer) 
    Const conDuplicateKey = 3022 
    Dim strMsg As String 
 
    If DataErr = conDuplicateKey Then 
        Response = acDataErrContinue 
        strMsg = "Each employee record must have a unique " _ 
            &amp; "employee ID number. Please recheck your data." 
        MsgBox strMsg 
    End If 
End Sub
```


 

 

 
En el ejemplo siguiente se muestra cómo se puede reemplazar un mensaje de error predeterminado por un mensaje de error personalizado.
 

 
 **Código de ejemplo proporcionado por:**
 
![MVP colaborador](images/odc_OfficeTA_33px_MVPContrib.jpg)
 
 Bill Jelen, [MrExcel.com](http://www.mrexcel.com/)
 

 



```
Private Sub Form_Error(DataErr As Integer, Response As Integer)
    Select Case DataErr
        Case 2113
            MsgBox "Only numbers are acceptable in this box", vbCritical, "Call 1-800-123-4567"
            Response = acDataErrContinue
        Case 2237
            MsgBox "You can only choose from the dropdown box"
            Response = acDataErrContinue
        Case 3022
            MsgBox "You entered a value that exists already in another record"
            Response = acDataErrContinue
            SSN.Value = SSN.OldValue
        Case 3314
            MsgBox "The DOH is required, so you cannot leave this field empty"
            Response = acDataErrContinue
        Case Else
            Response = acDataErrDisplay
    End Select
    ActiveControl.Undo
End Sub
```


## Acerca de los colaboradores
<a name="AboutContributors"> </a>

UtterAccess es el principal foro de ayuda y wiki sobre Microsoft Access. Haga clic aquí para unirse.
 

 
Holy Macro! Books publica libros de entretenimiento para personas que usan Microsoft Office. Puede consultar el catálogo completo en MrExcel.com.
 

 

## Vea también
<a name="AboutContributors"> </a>


#### Conceptos


 
 [Objeto Form](72ef9219-142b-b690-b696-3eba9a5d4522.md)
#### Otros recursos


 
 [Miembros del objeto Form](e1976b58-28ca-8f76-cdf3-6732cb06ce6c.md)