
# QueryTables.Add 方法 (Excel)

會建立新的查詢表。
 


## 語法

 *運算式*  . **Add**( ** *Connection* **, ** *Destination* **, ** *Sql* ** )
 

 
 *運算式*  代表 **QueryTables** 物件的變數。
 

 

### 參數



|**名稱**|**必要/選用**|**資料類型**|**描述**|
|:-----|:-----|:-----|:-----|
| _Connection_|必要|**Variant**| 查詢表的資料來源。可以為下列其中一項： 包含 OLE DB 或 ODBC 連接字串的字串。ODBC 連接字串具有 "ODBC;<連接字串>" 的格式。 **QueryTable** 物件，即查詢資訊最初複製的來源，其中包括連接字串及 SQL 文字，但不包括 **Destination** 範圍。如果指定 **QueryTable** 物件，則會忽略 **Sql** 引數。 ADO 或 DAO **Recordset** 物件。資料是從 ADO 或 DAO 資料錄集讀取而來。Microsoft Excel 會保留資料錄集，一直到刪除查詢表或變更連接為止。產生的查詢表無法編輯。 Web 查詢。這是一個格式為 "URL;<url>" 的字串，其中的 "URL;" 為必要項，但是未本土化；字串的其他部分則是做為 Web 查詢的 URL。 資料尋找工具。這是一個格式為 "FINDER;<資料尋找工具檔案路徑>" 的字串，其中的 "FINDER;" 為必要項，但是未本土化；字串的其他部分則是資料尋找工具檔 (*.dqy 或 *.iqy) 的路徑及檔案名稱。此檔案是在執行 **Add** 方法時讀取的。後續呼叫查詢表的 ** [Connection](a576c5d2-113c-cbd0-1ad2-aa46591944de.md)** 屬性時，將會傳回以 "ODBC;" 或 "URL;" 開頭的適當字串。 文字檔。這是一個格式為 "TEXT;<文字檔路徑及名稱>" 的字串，其中的 TEXT 為必要項，但是未本土化。|
| _Destination_|必要|**Range**|位於查詢表目的範圍 (即將要放置所產生查詢表的範圍) 左上角的儲存格。目的範圍必須在包含運算式所指定之  **QueryTables** 物件的工作表上。|
| _Sql_|選用|**Variant**|要在 ODBC 資料來源上執行的 SQL 查詢字串。如果使用的是 ODBC 資料來源，可以選擇使用這個引數 (如果不在這裡指定這個引數，則應該在更新查詢表之前，使用查詢表的  **Sql** 屬性來設定這個引數)。如果指定 **QueryTable** 物件、文字檔或是 ADO 或 DAO **Recordset** 物件做為資料來源，則不能使用這個引數。|

### 傳回值

代表新查詢表的  ** [QueryTable](505b84ea-64b3-b4fe-741a-de6884eb69eb.md)** 物件。
 

 

## 註解

必須呼叫  ** [Refresh](445d74fb-1a9c-bba4-2d53-0ab0caa876da.md)** 方法之後，才會執行以此方法所建立的查詢。
 

 

## 範例

這個範例會根據 ADO 資料錄集建立查詢表。基於回溯相容性，這個範例將會保留現有的欄排序和篩選設定，以及版面配置資訊。
 

 

```
Dim cnnConnect As ADODB.Connection 
Dim rstRecordset As ADODB.Recordset 
 
Set cnnConnect = New ADODB.Connection 
cnnConnect.Open "Provider=SQLOLEDB;" &amp; _ 
    "Data Source=srvdata;" &amp; _ 
    "User ID=testac;Password=4me2no;" 
 
Set rstRecordset = New ADODB.Recordset 
rstRecordset.Open _ 
    Source:="Select Name, Quantity, Price From Products", _ 
    ActiveConnection:=cnnConnect, _ 
    CursorType:=adOpenDynamic, _ 
    LockType:=adLockReadOnly, _ 
    Options:=adCmdText 
 
With ActiveSheet.QueryTables.Add( _ 
        Connection:=rstRecordset, _ 
        Destination:=Range("A1")) 
    .Name = "Contact List" 
    .FieldNames = True 
    .RowNumbers = False 
    .FillAdjacentFormulas = False 
    .PreserveFormatting = True 
    .RefreshOnFileOpen = False 
    .BackgroundQuery = True 
    .RefreshStyle = xlInsertDeleteCells 
    .SavePassword = True 
    .SaveData = True 
    .AdjustColumnWidth = True 
    .RefreshPeriod = 0 
    .PreserveColumnInfo = True 
    .Refresh BackgroundQuery:=False 
End With
```

這個範例會將固定寬度的文字檔匯入到新的查詢表中。該文字檔的第一欄為五個字元寬度，而且是以文字形式匯入。第二欄為四個字元寬度，會予以略過。該文字檔的其餘部分則會匯入到第三欄，並且會套用通用格式。
 

 



```
Set shFirstQtr = Workbooks(1).Worksheets(1) 
Set qtQtrResults = shFirstQtr.QueryTables.Add( _ 
    Connection := "TEXT;C:\My Documents\19980331.txt", 
    Destination := shFirstQtr.Cells(1,1)) 
With qtQtrResults 
    .TextFileParsingType = xlFixedWidth 
    .TextFileFixedColumnWidths := Array(5,4) 
    .TextFileColumnDataTypes := _ 
        Array(xlTextFormat, xlSkipColumn, xlGeneralFormat) 
    .Refresh 
End With
```

這個範例會在現用工作表上建立新的查詢表。
 

 



```
sqlstring = "select 96Sales.totals from 96Sales where profit < 5" 
connstring = _ 
    "ODBC;DSN=96SalesData;UID=Rep21;PWD=NUyHwYQI;Database=96Sales" 
With ActiveSheet.QueryTables.Add(Connection:=connstring, _ 
        Destination:=Range("B1"), Sql:=sqlstring) 
    .Refresh 
End With
```


## 請參閱


#### 概念


 
 [QueryTables 物件](93511da3-598e-0aa3-fbc3-14bebff8838f.md)
#### 其他資源


 
 [QueryTables 物件成員](a573c0fc-befa-0ee6-68bb-627cb516f98c.md)